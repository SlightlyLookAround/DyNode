/// DyCore Interface.

enum DYCORE_ASYNC_EVENT_TYPE { PROJECT_SAVING, GENERAL_ERROR, GM_ANNOUNCEMENT, ON_FILES_DROPPED };
function DyCoreManager() constructor {
    // DyCore Step function.
    static step = function() {
        // Manage async events.
        if(DyCore_has_async_event()) {
            var _async_event = DyCore_get_async_event();
            if(_async_event == "") {
                show_debug_message("!Warning: DyCore async event is empty.");
                announcement_error("DyCore async events parsed failed.");
                return;
            }
            else {
                do_async_events(json_parse(_async_event));
            }
        }
    }

    static do_async_events = function(event) {
        show_debug_message(event);
        switch(event[$ "type"]) {
            case DYCORE_ASYNC_EVENT_TYPE.PROJECT_SAVING:
                project_save_callback(event);
                break;
            case DYCORE_ASYNC_EVENT_TYPE.GENERAL_ERROR:
                announcement_error(i18n_get("anno_dycore_error", event[$ "content"]));
                break;
            case DYCORE_ASYNC_EVENT_TYPE.GM_ANNOUNCEMENT:
                var _anno_content = json_parse(event[$ "content"]);
                var _msg = _anno_content[$ "msg"];
                var _args = _anno_content[$ "args"];
                var _last_time = variable_struct_exists(_anno_content, "lastTime")
                    ? _anno_content[$ "lastTime"]
                    : undefined;
                switch(event[$ "status"]) {
                    case 0:
                        if(is_undefined(_last_time))
                            announcement_play(i18n_get(_msg, _args));
                        else
                            announcement_play(i18n_get(_msg, _args), _last_time);
                        break;
                    case 1:
                        if(is_undefined(_last_time))
                            announcement_warning(i18n_get(_msg, _args));
                        else
                            announcement_warning(i18n_get(_msg, _args), _last_time);
                        break;
                    case 2:
                        if(is_undefined(_last_time))
                            announcement_error(i18n_get(_msg, _args));
                        else
                            announcement_error(i18n_get(_msg, _args), _last_time);
                        break;
                    default:
                        announcement_error("Unknown GM announcement type from DyCore.");
                        break;
                }
                break;
            case DYCORE_ASYNC_EVENT_TYPE.ON_FILES_DROPPED:
                window_on_files_dropped(event[$ "content"]);
                break;
            default:
                show_debug_message("!Warning: Unknown dycore async event type.");
                break;
        }
    }
}

function dyc_init() {
    var result = DyCore_init(window_handle(), program_directory);
    if(result != "success") {
        show_error($"DyCore Initialized Failed. Result: {result}", true);
    }
    /// Add sprites data.
    // Add sprNote2.
    var uv = sprite_get_uvs(sprNote2, 0);
    dyc_add_sprite_data({
        name: "sprNote",
        uv00: uv[0],
        uv01: uv[1],
        uv10: uv[2],
        uv11: uv[3],
        width: sprite_get_width(sprNote2),
        height: sprite_get_height(sprNote2),
        type: 1, // SEG-3
        data: [22, 22],
        paddingLR: _note_get_lrpadding_total(0),
        paddingTop: 0,
        paddingBottom: 0
    });

    // Add sprChain.
    var uv = sprite_get_uvs(sprChain, 0);
    dyc_add_sprite_data({
        name: "sprChain",
        uv00: uv[0],
        uv01: uv[1],
        uv10: uv[2],
        uv11: uv[3],
        width: sprite_get_width(sprChain),
        height: sprite_get_height(sprChain),
        type: 2, // SEG-5
        data: [21, 78, 19],
        paddingLR: _note_get_lrpadding_total(1),
        paddingTop: 0,
        paddingBottom: 0
    });

    // Add sprHoldEdge
    var uv = sprite_get_uvs(sprHoldEdge, 0);
    dyc_add_sprite_data({
        name: "sprHoldEdge",
        uv00: uv[0],
        uv01: uv[1],
        uv10: uv[2],
        uv11: uv[3],
        width: sprite_get_width(sprHoldEdge),
        height: sprite_get_height(sprHoldEdge),
        type: 3, // Slice-9
        data: [32, 33, 53, 52],
        paddingLR: _note_get_lrpadding_total(2),
        paddingTop: 13,
        paddingBottom: 26,
    });

    // Add sprHold
    var uv = sprite_get_uvs(sprHold, 0);
    dyc_add_sprite_data({
        name: "sprHold",
        uv00: uv[0],
        uv01: uv[1],
        uv10: uv[2],
        uv11: uv[3],
        width: sprite_get_width(sprHold),
        height: sprite_get_height(sprHold),
        type: 4, // Repeat-vert
        data: [],
        paddingLR: 0,
        paddingTop: 0,
        paddingBottom: 0,
    });

    // Add sprHoldGrey
    var uv = sprite_get_uvs(sprHoldGrey, 0);
    dyc_add_sprite_data({
        name: "sprHoldGrey",
        uv00: uv[0],
        uv01: uv[1],
        uv10: uv[2],
        uv11: uv[3],
        width: sprite_get_width(sprHoldGrey),
        height: sprite_get_height(sprHoldGrey),
        type: 0, // Normal
        data: [],
        paddingLR: 0,
        paddingTop: 0,
        paddingBottom: 0,
    });
}

/// @param {Id.Buffer} buffer 
/// @param {Struct.sNoteProp} noteProp 
function dyc_note_serialization(buffer, noteProp) {
    noteProp.bitwrite(buffer);
}

function dyc_note_deserialization(buffer) {
    return new sNoteProp().bitread(buffer);
}

/// @param {Struct.sNoteProp} noteProp 
function dyc_update_note(noteProp, record = false, recursive = false) {
    if(!is_struct(noteProp)) {
        announcement_error("Error in dyc_update_note: noteProp is not a struct.");
        return;
    }

    // Check if noteProp is sNoteProp type.
    if(!variable_struct_exists(noteProp, "copy"))
        noteProp = new sNoteProp(noteProp);
    else
        noteProp = noteProp.copy();

    var noteID = noteProp.noteID;
    if(!dyc_note_exists(noteID)) {
        show_debug_message("!Warning: dyc_update_note called with non-existing noteID: " + noteID);
        return;
    }

    // Modified notes must be re-pulled from the backend, not the frame cache.
    dyc_active_props_cache_invalidate();

    var origProp = -1;
    if(record) origProp = dyc_get_note(noteID);

    static buffer = buffer_create(1024, buffer_grow, 1);
    dyc_note_serialization(buffer, noteProp);
    var result = DyCore_modify_note(buffer_get_address(buffer));

    if(result == 0 && record) {
        operation_step_add(OPERATION_TYPE.MOVE, origProp, noteProp);
    }

    if(!recursive) {
        if(noteProp.noteType == NOTE_TYPE.HOLD) {
            // Sync the subnote's data.
            var subNote = dyc_get_note(noteProp.subNoteID);
            subNote.position = noteProp.position;
            subNote.side = noteProp.side;
            subNote.width = noteProp.width;
            subNote.beginTime = noteProp.time;
            subNote.time = noteProp.time + noteProp.lastTime;
            
            dyc_update_note(subNote, false, true);
        }
        else if(noteProp.noteType == NOTE_TYPE.SUB) {
            // Sync the father's data.
            var parentNote = dyc_get_note(noteProp.subNoteID);
            parentNote.lastTime = noteProp.time - parentNote.time;
    
            dyc_update_note(parentNote, false, true);
        }
    }

    // Pull to active notes immediately.
    if(result == 0 && noteProp.noteType != NOTE_TYPE.SUB) {
        if(note_is_activated(noteID)) {
            var noteObj = note_get_instance(noteID);
            noteObj.pull_prop();
        }
    }

    if(result < 0)
        throw "Unknown error in dyc_update_note.";
}

/// @param {Struct.sNoteProp} noteProp 
function dyc_create_note(noteProp, record = false) {
    static buffer = buffer_create(1024, buffer_grow, 1);
    noteProp.bitwrite(buffer);
    var result = DyCore_insert_note(buffer_get_address(buffer));
    if(result < 0) {
        throw "Unknown error in dyc_create_note.";
    }
    if(record) {
        operation_step_add(OPERATION_TYPE.ADD, noteProp.copy(), -1);
    }
}

/// @description Get note by noteID.
/// @param {String} noteID The note ID.
/// @returns {Struct.sNoteProp} The note struct or undefined if not found.
function dyc_get_note(noteID) {
    static propBuffer = buffer_create(1024, buffer_grow, 1);
    var result = DyCore_get_note(noteID, buffer_get_address(propBuffer));
    if (result == 0) {
        return dyc_note_deserialization(propBuffer);
    }
    if (result == -1) {
        show_debug_message("!Warning: dyc_get_note failed to find noteID: " + noteID);
    }
    return undefined;
}

/// @description Get note at notes array's index.
/// @param {Real} index The index of the note in the notes array.
/// @returns {Struct.sNoteProp} The note struct or undefined if not found.
function dyc_get_note_at_index(index) {
    DyCore_sort_notes();
    static propBuffer = buffer_create(1024, buffer_grow, 1);
    var result = DyCore_get_note_at_index(index, buffer_get_address(propBuffer));
    if (result == 0) {
        return dyc_note_deserialization(propBuffer);
    }
    return undefined;
}

/// @description Get note at notes array's index directly.
/// This will bypass the OutOfOrder flag.
/// @param {Real} index The index of the note in the notes array.
/// @returns {Struct.sNoteProp} The note struct or undefined if not found.
function dyc_get_note_at_index_direct(index) {
    static propBuffer = buffer_create(1024, buffer_grow, 1);
    var result = DyCore_get_note_at_index_direct(index, buffer_get_address(propBuffer));
    if (result == 0) {
        return dyc_note_deserialization(propBuffer);
    }
    return undefined;
}

/// @description Get note time at notes array's index.
/// @param {Real} index The index of the note in the notes array.
/// @returns {Real} The time of the note or undefined if not found.
function dyc_get_note_time_at_index(index) {
    DyCore_sort_notes();
    var result = DyCore_get_note_time_at_index(index);
    if (result != -999999) {
        return result;
    }
    return undefined;
}

/// @description Get note time at notes array's index.
/// @param {Real} index The index of the note in the notes array.
/// @returns {Real} The time of the note or undefined if not found.
function dyc_get_note_id_at_index(index) {
    DyCore_sort_notes();
    var result = DyCore_get_note_id_at_index(index);
    if (result != "") {
        return result;
    }
    return undefined;
}

/// @description Get the index of the note in the notes array by noteID.
/// @param {String} noteID The note ID.
/// @returns {Real} The index of the note in the notes array or -1 if not found.
function dyc_get_note_array_index(noteID) {
    return DyCore_get_note_array_index(noteID);
}

function dyc_note_exists(noteID) {
    return DyCore_note_exists(noteID) >= 0;
}

function dyc_get_note_count() {
    return DyCore_get_note_count();
}

function dyc_chart_import_xml(filePath, importInfo, importTiming) {
    var _result = DyCore_chart_import_xml(filePath, importInfo, importTiming);
    if (_result == 1) {
        announcement_error("dym_import_failed");
        return -1;
    } else if (_result == 2) {
        announcement_warning("bad_dym_chart_format", 10000);
        return -1;
    }
    if(_result != 0) return -1;

    show_debug_message("Load XML file completed.");
    analytics_track_event("ChartImportXML", { result: _result });
    dyc_active_props_cache_invalidate();
    return 0;
}

function dyc_chart_import_dy(filePath, importInfo, importTiming) {
    var _result = DyCore_chart_import_dy(filePath, importInfo, importTiming);
    if (_result == 1) {
        announcement_error("dym_import_failed");
        return -1;
    } else if (_result == 2) {
        announcement_warning("bad_dym_chart_format", 10000);
        return -1;
    }
    if(_result != 0) return -1;

    show_debug_message("Load DY file completed.");
    analytics_track_event("ChartImportDY", { result: _result });
    dyc_active_props_cache_invalidate();
    return 0;
}

function dyc_chart_import_dy_get_remix() {
    try {
        return json_parse(DyCore_chart_import_dy_get_remix());
    } catch (e) {
        show_debug_message("Error parsing DY remix: " + string(e));
        return undefined;
    }
}

/// @param {Real} fixError The fix-error offset.
function dyc_chart_export_xml(filePath, isDym, fixError) {
    return DyCore_chart_export_xml(filePath, isDym, fixError);
}

function dyc_project_load(filePath) {
    return DyCore_project_load(filePath);
}

function dyc_chart_import_dyn(filePath, importInfo, importTiming) {
    var _result = DyCore_chart_import_dyn(filePath, importInfo, importTiming);
    dyc_active_props_cache_invalidate();
    return _result;
}

/// @returns {Any} The chart metadata struct.
function dyc_chart_get_metadata() {
    static _metadata = {};
    static _lastModified = -1;
    try {
        var _modified = DyCore_get_chart_metadata_last_modified_time();
        if(_modified != _lastModified) {
            _lastModified = _modified;
            _metadata = json_parse(DyCore_get_chart_metadata());
        }
        return _metadata;
    } catch (e) {
        show_debug_message("Error parsing chart metadata: " + string(e));
        return undefined;
    }
}

function dyc_chart_set_metadata(metadata) {
    try {
        var _metadataStr = json_stringify(metadata);
        return DyCore_set_chart_metadata(_metadataStr);
    } catch (e) {
        show_debug_message("Error setting chart metadata: " + string(e));
        return false;
    }
}

function dyc_chart_set_title(title) {
    try {
        var _metadata = dyc_chart_get_metadata();
        _metadata.title = title;
        return dyc_chart_set_metadata(_metadata);
    } catch (e) {
        show_debug_message("Error setting chart title: " + string(e));
        return false;
    }
}

function dyc_chart_set_difficulty(difficulty) {
    try {
        var _metadata = dyc_chart_get_metadata();
        _metadata.difficulty = difficulty;
        return dyc_chart_set_metadata(_metadata);
    } catch (e) {
        show_debug_message("Error setting chart difficulty: " + string(e));
        return false;
    }
}

function dyc_chart_set_sidetype(sideType) {
    try {
        var _metadata = dyc_chart_get_metadata();
        _metadata.sideType = sideType;
        return dyc_chart_set_metadata(_metadata);
    } catch (e) {
        show_debug_message("Error setting chart side type: " + string(e));
        return false;
    }
}

function dyc_chart_get_title() {
    try {
        var _metadata = dyc_chart_get_metadata();
        return _metadata.title;
    } catch (e) {
        show_debug_message("Error getting chart title: " + string(e));
        return undefined;
    }
}

function dyc_chart_get_difficulty() {
    try {
        var _metadata = dyc_chart_get_metadata();
        return _metadata.difficulty;
    } catch (e) {
        show_debug_message("Error getting chart difficulty: " + string(e));
        return undefined;
    }
}

function dyc_chart_get_sidetype() {
    try {
        var _metadata = dyc_chart_get_metadata();
        return _metadata.sideType;
    } catch (e) {
        show_debug_message("Error getting chart side type: " + string(e));
        return undefined;
    }
}

function dyc_chart_get_path() {
    try {
        var _path = DyCore_get_chart_path();
        return json_parse(_path);
    } catch (e) {
        show_debug_message("Error parsing chart path: " + string(e));
        return undefined;
    }
}

function dyc_project_get_metadata() {
    try {
        var _metadata = DyCore_get_project_metadata();
        return json_parse(_metadata);
    } catch (e) {
        show_debug_message("Error parsing project metadata: " + string(e));
        return undefined;
    }
}

/// @returns {Array<Struct.sTimingPoint>} 
function dyc_get_timingpoints() {
    static _timingpoints = [];
    static _lastModifiedTime = -1;
    var _lastModified = DyCore_get_timing_points_last_modified_time();
    if (_lastModified != _lastModifiedTime) {
        _lastModifiedTime = _lastModified;
        try {
            _timingpoints = json_parse(DyCore_get_timing_array_string());
            for(var i=0, l=array_length(_timingpoints); i<l; i++) {
                var data = _timingpoints[i];
                _timingpoints[i] = build_timingpoint_from_data(data);
                delete data;
            }
        } catch (e) {
            show_debug_message("Error parsing timing points: " + string(e));
        }
    }
    return _timingpoints;
}

/// @description Get the timing point containing the specified time.
/// @param {Real} time Time to query.
/// @returns {Struct.sTimingPoint} 
function dyc_get_timingpoint_at(time) {
    var _timingPoint = DyCore_get_timing_point_at(time);
    if (_timingPoint == "") {
        show_debug_message($"Cannot get timingpoint at {time}.");
        return undefined;
    }
    try {
        return build_timingpoint_from_data(json_parse(_timingPoint));
    } catch (e) {
        show_debug_message("Error parsing timing point: " + string(e));
        return undefined;
    }
}

function dyc_get_timingpoints_count() {
    return DyCore_get_timing_points_count();
}

/// @param {Struct.sTimingPoint} timingPoint 
function dyc_insert_timingpoint(timingPoint) {
    try {
        return DyCore_insert_timing_point(json_stringify(timingPoint));
    } catch (e) {
        show_debug_message("Error inserting timing point: " + string(e));
        return -1;
    }
}

function dyc_timingpoints_sort() {
    return DyCore_timing_points_sort();
}

function dyc_timingpoints_reset() {
    return DyCore_timing_points_reset();
}

function dyc_timingpoints_delete_at(time) {
    return DyCore_delete_timing_point_at_time(time);
}

function dyc_timingpoints_change(time, timingPoint) {
    return DyCore_timing_points_change(time, json_stringify(timingPoint));
}

function dyc_timingpoints_add_offset(offset) {
    return DyCore_timing_points_add_offset(offset);
}

// =============================================================================
// Color Keyframe API wrappers
// =============================================================================

function dyc_color_keyframes_count() {
    return DyCore_color_keyframes_count();
}

/// @returns {Array<Struct>} Array of color keyframe structs {time, color, interp}.
function dyc_color_keyframes_get_all() {
    var _json = DyCore_color_keyframes_get_all();
    if (_json == "" || _json == "[]") return [];
    try {
        return json_parse(_json);
    } catch (e) {
        show_debug_message("Error parsing color keyframes: " + string(e));
        return [];
    }
}

/// @param {Real} index
/// @returns {Struct|undefined}
function dyc_color_keyframe_get(index) {
    var _json = DyCore_color_keyframe_get(index);
    if (_json == "" || _json == "{}") return undefined;
    try {
        return json_parse(_json);
    } catch (e) {
        return undefined;
    }
}

/// @param {Real} _time
/// @param {Real} _color 0xRRGGBB
/// @param {Real} _interp 0=SmoothHSV 1=SmoothRGB 2=Instant
function dyc_color_keyframe_insert(_time, _color, _interp) {
    return DyCore_color_keyframe_insert(_time, _color, _interp);
}

/// @param {Real} _time
function dyc_color_keyframe_delete(_time) {
    return DyCore_color_keyframe_delete(_time);
}

/// @param {Real} _time
/// @param {Real} _newColor 0xRRGGBB
/// @param {Real} _newInterp
function dyc_color_keyframe_change(_time, _newColor, _newInterp) {
    return DyCore_color_keyframe_change(_time, _newColor, _newInterp);
}

function dyc_color_keyframes_reset() {
    return DyCore_color_keyframes_reset();
}

/// @param {Real} _time
/// @param {Real} _baseColor 0xRRGGBB fallback
/// @returns {Real} Resolved color at the given time.
function dyc_color_keyframe_resolve(_time, _baseColor) {
    return DyCore_color_keyframe_resolve(_time, _baseColor);
}

/// @returns {Bool} Whether the color timeline is enabled.
function dyc_color_timeline_get_enabled() {
    return DyCore_color_timeline_get_enabled() > 0;
}

/// @param {Bool} enabled
function dyc_color_timeline_set_enabled(enabled) {
    DyCore_color_timeline_set_enabled(enabled ? 1 : 0);
}

function dyc_project_get_version() {
    return DyCore_get_project_version();
}

/// @description Set whether a objEditor instance is ready.
/// @param {Bool} ready
/// @returns {Real}
function dyc_editor_set_ready(ready) {
    return DyCore_gmeditor_set_ready(ready);
}

/// @description Check whether a objEditor instance is ready.
/// @returns {Bool}
function dyc_editor_get_ready() {
    return DyCore_gmeditor_get_ready();
}

/// @description Synchronize objEditor state to DyCore.
/// @param {Struct} states
/// @returns {Real}
function dyc_editor_sync_states(states) {
    return DyCore_gmeditor_sync_states(json_stringify(states));
}

function dyc_get_active_notes(nowTime, noteSpeed) {
    static _activeNotes = [];
    static lastUpdateTime = -1;
    static buffer = buffer_create(1024 * 1024, buffer_fixed, 1);

    if(lastUpdateTime != global.frameCurrentTime) {
        lastUpdateTime = global.frameCurrentTime;
        delete _activeNotes;
        _activeNotes = [];
    }
    else return _activeNotes;

    var boundSize = DyCore_cac_active_notes(nowTime, noteSpeed);
    if(boundSize > buffer_get_size(buffer)) {
        buffer_resize(buffer, boundSize);
        buffer_set_used_size(buffer, boundSize);
    }

    DyCore_get_active_notes(buffer_get_address(buffer));
    buffer_seek(buffer, buffer_seek_start, 0);
    var count = buffer_read(buffer, buffer_u32);

    array_resize(_activeNotes, count);
    for(var i = 0; i < count; i++) {
        _activeNotes[i] = buffer_read(buffer, buffer_string);
    }

    return _activeNotes;
}

function dyc_update_active_notes() {
    DyCore_cac_active_notes(objMain.nowTime, objMain.playbackSpeed);
}

/// @description Refresh the per-frame active note props cache.
/// Call once per frame from objMain Step_1, after notes are activated.
/// One batched DyCore call replaces hundreds of per-note round trips.
function dyc_active_props_cache_refresh() {
    if(global.dycActivePropsFrame == global.frameCurrentTime) return;
    global.dycActivePropsFrame = global.frameCurrentTime;
    global.dycActivePropsCache = undefined;

    static buffer = buffer_create(1024 * 1024, buffer_fixed, 1);
    var boundSize = DyCore_get_active_notes_props_bound();
    if(boundSize > buffer_get_size(buffer)) {
        buffer_resize(buffer, boundSize);
        buffer_set_used_size(buffer, boundSize);
    }
    if(DyCore_get_active_notes_props(buffer_get_address(buffer)) != 0) return;

    buffer_seek(buffer, buffer_seek_start, 0);
    var count = buffer_read(buffer, buffer_u32);
    var _cache = {};
    for(var i = 0; i < count; i++) {
        var _prop = new sNoteProp();
        _prop.side = buffer_read(buffer, buffer_u32);
        _prop.noteType = buffer_read(buffer, buffer_u32);
        _prop.time = buffer_read(buffer, buffer_f64);
        _prop.width = buffer_read(buffer, buffer_f64);
        _prop.position = buffer_read(buffer, buffer_f64);
        _prop.lastTime = buffer_read(buffer, buffer_f64);
        _prop.beginTime = buffer_read(buffer, buffer_f64);
        _prop.noteID = buffer_read(buffer, buffer_string);
        _prop.subNoteID = buffer_read(buffer, buffer_string);
        _cache[$ _prop.noteID] = _prop;
    }
    global.dycActivePropsCache = _cache;
}

/// @description Get a note's props from the per-frame cache.
/// @param {String} noteID
/// @returns {Struct.sNoteProp} The note struct or undefined on miss.
function dyc_active_props_cache_get(noteID) {
    var _cache = global.dycActivePropsCache;
    if(!is_struct(_cache)) return undefined;
    return _cache[$ noteID];
}

/// @description Invalidate the per-frame props cache after any note change.
function dyc_active_props_cache_invalidate() {
    global.dycActivePropsCache = undefined;
}

/// @description This function will not update active notes.
function dyc_get_lasting_holds() {
    static _lastingHolds = [];
    static lastUpdateTime = -1;
    static buffer = buffer_create(1024 * 1024, buffer_fixed, 1);

    if(lastUpdateTime != global.frameCurrentTime) {
        lastUpdateTime = global.frameCurrentTime;
        delete _lastingHolds;
        _lastingHolds = [];
    }
    else return _lastingHolds;

    var boundSize = DyCore_get_active_notes_bound();
    if(boundSize > buffer_get_size(buffer)) {
        buffer_resize(buffer, boundSize);
        buffer_set_used_size(buffer, boundSize);
    }

    DyCore_get_lasting_holds(buffer_get_address(buffer));
    buffer_seek(buffer, buffer_seek_start, 0);
    var count = buffer_read(buffer, buffer_u32);

    array_resize(_lastingHolds, count);
    for(var i = 0; i < count; i++) {
        _lastingHolds[i] = buffer_read(buffer, buffer_string);
    }

    return _lastingHolds;
}

function dyc_add_sprite_data(data) {
    try {
        return DyCore_add_sprite_data(json_stringify(data));
    } catch (e) {
        show_debug_message("Error adding sprite data: " + string(e));
        return -1;
    }
}

function dyc_get_note_index_lowerbound(time) {
    return DyCore_get_note_index_lower_bound(time);
}

function dyc_get_note_index_upperbound(time) {
    return DyCore_get_note_index_upper_bound(time);
}

/// @description Get the first note index on a side at or after an array index.
/// @param {Real} side The note side to search for.
/// @param {Real} index The notes array index at which to begin searching.
/// @param {Real} [untilTime=-1] The inclusive maximum note time, or -1 for no limit.
/// @returns {Real} The matching note index, or -1 if no note was found.
function dyc_get_note_index_on_side_after_index(side, index, untilTime = -1) {
    return DyCore_get_note_index_on_side_after_index(side, index, untilTime);
}

function dyc_get_open_filename(filter, filename, dir, title) {
    var result = DyCore_get_open_filename(filter, filename, dir, title);

    if(result == "terminated") return "";
    else if(result == "") {
        announcement_error("Opening file save dialogue failed.");
        return result;
    }

    return result;
}

function dyc_get_save_filename(filter, filename, dir, title) {
    var result = DyCore_get_save_filename(filter, filename, dir, title);

    if(result == "terminated") return "";
    else if(result == "") {
        announcement_error("Opening file save dialogue failed.");
        return result;
    }

    return result;
}

function dyc_show_question(text) {
    var result = DyCore_show_question(text);
    return result > 0 ? true : false;
}

function dyc_get_string(prompt, default_text) {
    var result = DyCore_get_string(prompt, default_text);
    if(result == "<%$><.3>TERMINATED<!#><##>") return "";
    else if(result == "") {
        announcement_error("Opening input dialog failed.");
        return result;
    }
    return result;
}

function dyc_enable_ime() {
    return DyCore_enable_ime();
}

function dyc_disable_ime() {
    return DyCore_disable_ime();
}

function dyc_random_range(min, max) {
    return DyCore_random_range(min, max);
}

function dyc_random(r) {
    return dyc_random_range(0, r);
}

function dyc_irandom_range(min, max) {
    return DyCore_irandom_range(min, max);
}

function dyc_irandom(r) {
    return dyc_irandom_range(0, r);
}

function dyc_has_timing_point_at_time(time) {
    return DyCore_has_timing_point_at_time(time) > 0;
}

function dyc_ffmpeg_start_recording(filename, musicPath, width, height, fps, muiscOffset) {
    return DyCore_ffmpeg_start_recording(json_stringify(
        {
            filename: filename,
            musicPath: musicPath,
            width: int64(width),
            height: int64(height),
            fps: int64(fps),
            musicOffset: muiscOffset
        }
    ));
}

/// @description Solve natural spline interpolation.
/// @param {Array<Real>} xIn Input x array.
/// @param {Array<Real>} fIn Input f(x) array.
/// @param {Array<Real>} xOut Output x array.
/// @returns {Array<Real>} Output f(x) array.
function dyc_solve_natural_spline(xIn, fIn, xOut) {
    var bufferXFIn = buffer_create(array_length(xIn) * 8 * 2, buffer_fixed, 1);
    var bufferXOut = buffer_create(array_length(xOut) * 8, buffer_fixed, 1);
    var bufferFOut = buffer_create(array_length(xOut) * 8, buffer_fixed, 1);
    var params = json_stringify({
        n_points: array_length(xIn),
        n_query: array_length(xOut)
    });

    var fillInBuffer = function(buffer, dataArray, offset = 0) {
        buffer_seek(buffer, buffer_seek_start, offset);
        for(var i = 0; i < array_length(dataArray); i++) {
            buffer_write(buffer, buffer_f64, dataArray[i]);
        }
    };
    fillInBuffer(bufferXFIn, xIn);
    fillInBuffer(bufferXFIn, fIn, array_length(xIn) * 8);
    fillInBuffer(bufferXOut, xOut);

    DyCore_solve_natural_spline(
        buffer_get_address(bufferXFIn),
        buffer_get_address(bufferXOut),
        buffer_get_address(bufferFOut),
        params
    );

    var fOut = array_create(array_length(xOut), 0);
    buffer_seek(bufferFOut, buffer_seek_start, 0);
    buffer_set_used_size(bufferFOut, array_length(xOut) * 8);
    for(var i = 0; i < array_length(xOut); i++) {
        fOut[i] = buffer_read(bufferFOut, buffer_f64);
    }

    return fOut;
}

/// @description Get note hash string.
/// @param {String} noteID The note ID.
/// @param {Bool} includeID Whether to include noteID in the hash.
/// @returns {String} The note hash string.
function dyc_get_note_hash(noteID, includeID) {
    return DyCore_get_note_hash(noteID, includeID ? 1 : 0);
}

// ---------------------------------------------------------------------------
// Batch operations (parallel C++ via DyCore)
// ---------------------------------------------------------------------------

/// @description Parallel fix out-of-screen notes. Clamps position to [0, 5].
/// @returns {Real} Number of fixed notes.
function dyc_editor_fix_notes() {
    return DyCore_editor_fix_notes();
}

/// @description Parallel timing fix: rescale notes in a timing segment after BPM change.
/// @param {Struct} tpBefore Old timing point (needs .time, .beatLength).
/// @param {Struct} tpAfter New timing point (needs .time, .beatLength).
/// @returns {Real} Affected count (positive) or negative if cross-boundary warning.
function dyc_timing_fix(tpBefore, tpAfter) {
    var timingPoints = dyc_get_timingpoints();
    var l = array_length(timingPoints);
    var at = -1;
    for (var i = 0; i < l; i++)
        if (timingPoints[i].time == tpBefore.time) { at = i; break; }
    var nextTime = (at + 1 == l) ? -1 : timingPoints[at + 1].time;
    return DyCore_timing_fix(
        tpBefore.time, tpBefore.beatLength,
        tpAfter.time, tpAfter.beatLength,
        nextTime
    );
}

/// @description Parallel chart randomize. Writes original props to buffer for undo.
/// @param {Id.Buffer} outBuffer Buffer to receive original props for undo.
/// @returns {Real} Number of randomized notes.
function dyc_chart_randomize(outBuffer) {
    return DyCore_chart_randomize(buffer_get_address(outBuffer));
}

/// @description Convert absolute time (ms) to 1-based bar position using segment lookup table.
/// @param {Real} time Absolute time in milliseconds.
/// @returns {Real} Bar position (1-based, fractional).
function dyc_time_to_bar(time) {
    return DyCore_time_to_bar(time);
}

/// @description Convert 1-based bar position to absolute time (ms) using segment lookup table.
/// @param {Real} bar Bar position (1-based, fractional).
/// @returns {Real} Absolute time in milliseconds.
function dyc_bar_to_time(bar) {
    return DyCore_bar_to_time(bar);
}

/// @description Add a signed bar delta to an absolute time, handling timing point boundaries.
/// @param {Real} time Absolute time in milliseconds.
/// @param {Real} deltaBars Signed bar delta.
/// @returns {Real} New absolute time in milliseconds.
function dyc_time_add_bar_delta(time, deltaBars) {
    return DyCore_time_add_bar_delta(time, deltaBars);
}

// ---------------------------------------------------------------------------
// Batch operations: trianglify, dedup, sampling, beatlines
// ---------------------------------------------------------------------------

/// @description Parallel trianglify point animation update.
/// @param {Id.Buffer} pointsBuf Buffer with point data [u32 count][count × (f64 x,y,vx,vy)].
/// @param {Real} dt Delta time in seconds.
/// @param {Real} width Boundary width.
/// @param {Real} height Boundary height.
/// @returns {Real} 0 on success.
function dyc_trianglify_step(pointsBuf, dt, width, height) {
    return DyCore_trianglify_step(buffer_get_address(pointsBuf), dt, width, height);
}

/// @description Find duplicate notes by content hash (excluding noteID).
/// @returns {Array<String>} Array of duplicate noteIDs.
function dyc_find_duplicate_notes() {
    var _json = DyCore_find_duplicate_notes();
    return json_parse(_json);
}

/// @description Batch note sampling with interpolation.
/// @param {Id.Buffer} cpBuf Control points buffer.
/// @param {Real} beatDiv Beat division.
/// @param {Real} mode 0=linear, 1=cosine, 2=catmull-rom.
/// @param {Id.Buffer} outBuf Output buffer.
/// @returns {Real} Number of sample records.
function dyc_sample_notes(cpBuf, beatDiv, mode, outBuf) {
    return DyCore_sample_notes(buffer_get_address(cpBuf), beatDiv, mode, buffer_get_address(outBuf));
}

/// @description Batch compute beatline geometry data.
/// @param {String} configJson JSON string with all parameters (nowTime, playbackSpeed, etc. + beatline config).
/// @param {Id.Buffer} outBuf Output buffer.
/// @returns {Real} Number of line descriptors.
function dyc_compute_beatlines(configJson, outBuf) {
    return DyCore_compute_beatlines(configJson, buffer_get_address(outBuf));
}
