
#macro RECORDING_FPS 60
#macro RECORDING_FPS_MAX 600
#macro RECORDING_RESOLUTION_W 1920
#macro RECORDING_RESOLUTION_H 1080

enum FFmpegPushFrameError {
    INVALID_ARGUMENT = -1,
    NOT_RECORDING = -2,
    PIPE_UNAVAILABLE = -3,
    PROCESS_HANDLE_INVALID = -4,
    PROCESS_EXITED = -5,
    PROCESS_WAIT_FAILED = -6,
    ENQUEUE_FAILED = -7,
    UNSUPPORTED_PLATFORM = -8,
}

function ffmpeg_push_frame_error_text(code) {
    switch(code) {
        case FFmpegPushFrameError.INVALID_ARGUMENT:
            return "Invalid frame data or frame size.";
        case FFmpegPushFrameError.NOT_RECORDING:
            return "Recording is not active.";
        case FFmpegPushFrameError.PIPE_UNAVAILABLE:
            return "FFmpeg pipe is unavailable.";
        case FFmpegPushFrameError.PROCESS_HANDLE_INVALID:
            return "FFmpeg process handle is invalid.";
        case FFmpegPushFrameError.PROCESS_EXITED:
            return "FFmpeg process exited before receiving the frame.";
        case FFmpegPushFrameError.PROCESS_WAIT_FAILED:
            return "Failed to query FFmpeg process state.";
        case FFmpegPushFrameError.ENQUEUE_FAILED:
            return "Failed to enqueue frame data.";
        case FFmpegPushFrameError.UNSUPPORTED_PLATFORM:
            return "push_frame is unsupported on this platform.";
        default:
            return "Unknown push_frame error.";
    }
}

function RecordManager() constructor {

    prepareRecording = false;
    recording = false;
    frameBuffer = -1;
    originalFPS = game_get_speed(gamespeed_fps);
    targetFilePath = "";

    static _get_surface_buffer_size = function(w, h) {
        return w * h * 4;
    }

    static start_recording = function(filename) {

        prepareRecording = false;

        if(recording) {
            show_debug_message("-- Already recording!");
            return;
        }

        if(!DyCore_ffmpeg_is_available()) {
            show_debug_message("-- FFMPEG not available!");
            return;
        }

        recording = true;
        global.timeManager.set_mode_fixed(RECORDING_FPS);
        originalFPS = game_get_speed(gamespeed_fps);
        game_set_speed(RECORDING_FPS_MAX, gamespeed_fps);
        resolution_set(RECORDING_RESOLUTION_W, RECORDING_RESOLUTION_H, false);
        display_reset(max(global.graphics.VSync, 2), false);
        dyc_video_set_sync_mode(true);
        targetFilePath = filename;
        
        var w = RECORDING_RESOLUTION_W;
        var h = RECORDING_RESOLUTION_H;
        var _fps = RECORDING_FPS;
        var musicPath = "";

        // Get music path.
        if(instance_exists(objMain)) {
            musicPath = get_absolute_path(filename_path(objManager.projectPath), objManager.musicPath);
        }

        var err = dyc_ffmpeg_start_recording(filename, musicPath, w, h, _fps, PLAYBACK_EMPTY_TIME / 1000);
        if(err != 0) {
            show_debug_message("-- Failed to start recording. Error code: " + string(err));
        } else {
            show_debug_message("-- Recording started: " + filename);
        }

        frameBuffer = buffer_create(_get_surface_buffer_size(w, h), buffer_fast, 1);
        show_debug_message($"-- Frame buffer created. Size: {_get_surface_buffer_size(w, h)} bytes");
        show_debug_message($"-- Recording at {w}x{h} @ {_fps} FPS");
        show_debug_message("-- Music path: " + musicPath);

        global.__InputManager.freeze();
    }

    static push_frame = function() {
        if(!recording) return;

        buffer_get_surface(frameBuffer, application_surface, 0);
        
        var err = DyCore_ffmpeg_push_frame(buffer_get_address(frameBuffer), buffer_get_size(frameBuffer));

        if(err < 0) {
            var errText = ffmpeg_push_frame_error_text(err);
            show_debug_message("-- Failed to push frame. Error code: " + string(err) + ". " + errText);
            announcement_task(i18n_get("recording_failed", ["[" + string(err) + "] " + errText]), 5000, "recording", ANNO_STATE.ERROR);
            global.recordManager.abort_recording();
            return;
        }

        announcement_task(
            i18n_get("recording_processing", 
                [RECORDING_RESOLUTION_W, RECORDING_RESOLUTION_H, RECORDING_FPS, 100 * (max(objMain.nowTime, 0) / objMain.musicLength),
                DyCore_ffmpeg_get_using_decoder()]
            ), 5000, "recording");
    }

    static abort_recording = function() {
        if(!recording) {
            show_debug_message("-- Not recording!");
            return;
        }
        // No need to call `DyCore_ffmpeg_finish_recording`.
        recording = false;
        post_recording();
        show_debug_message("-- Recording aborted.");
    }

    static finish_recording = function() {
        if(!recording) {
            show_debug_message("-- Not recording!");
            return;
        }

        DyCore_ffmpeg_finish_recording();
        recording = false;
        post_recording();
        show_debug_message("-- Recording finished.");
        announcement_task(i18n_get("recording_complete", [targetFilePath]), 5000, "recording", ANNO_STATE.COMPLETE);
    }

    static post_recording = function() {
        buffer_delete(frameBuffer);
        frameBuffer = -1;
        global.timeManager.set_mode_default();
        game_set_speed(originalFPS, gamespeed_fps);
        display_reset(global.graphics.AA, global.graphics.VSync);
        dyc_video_set_sync_mode(false);

        global.__InputManager.unfreeze();
    }

    static is_recording = function() {
        return recording || prepareRecording;
    }

    /// @description Release recording resources without restoring a closing UI.
    static cleanup = function() {
        prepareRecording = false;
        recording = false;
        try {
            DyCore_ffmpeg_finish_recording();
        } finally {
            if(buffer_exists(frameBuffer)) buffer_delete(frameBuffer);
            frameBuffer = -1;
        }
    }

}

function recording_default_filename() {
    return $"{dyc_chart_get_title()}_{difficulty_num_to_name(dyc_chart_get_difficulty())}_recording.mp4";
}

function recording_start(filename = "") {
    if(!DyCore_ffmpeg_is_available()) {
        announcement_warning("recording_no_ffmpeg");
        return;
    }


    if(filename == "") {
        var defaultFilename = recording_default_filename();
        filename = dyc_get_save_filename("Video File (*.mp4)|*.mp4", defaultFilename, objManager.projectPath, i18n_get("recording_savefile_dlg_title"));
    }
    if(filename == "") {
        show_debug_message("-- Recording cancelled (no filename).");
        return;
    }

    global.recordManager.prepareRecording = true;
    playview_start_replay(method({
        filename: filename
    }, function() {
        global.recordManager.start_recording(filename);
    }));
}

function _debug_start_record() {
    playview_start_replay(function() {
        global.recordManager.start_recording("test114514.mp4");
    });
}

function _debug_stop_record() {
    global.recordManager.finish_recording();
    show_debug_message("Recording stopped.");
}