
// Prevent flickering when seek the video to beginning.
global.__DyCore_Video_Preparing_Flag = false;
global.__DyCore_Video_FrameBuffer = -1;
global.__DyCore_Video_FrameSurface = -999;

function dyc_video_get_frame() {
    var buffSize = DyCore_video_get_buffer_size();
    var syncMode = dyc_video_get_sync_mode();

    if(!buffer_exists(global.__DyCore_Video_FrameBuffer) || buffer_get_size(global.__DyCore_Video_FrameBuffer) != buffSize) {
        if(buffSize <= 0)
            return -1;

        if(buffer_exists(global.__DyCore_Video_FrameBuffer))
            buffer_resize(global.__DyCore_Video_FrameBuffer, buffSize);
        else {
            global.__DyCore_Video_FrameBuffer = buffer_create(buffSize, buffer_fixed, 1);
        }
    }

    var updated;

    if(syncMode) {
        var deltaTime = global.timeManager.get_delta() / 1000000; // in seconds
        updated = DyCore_video_get_frame_sync(buffer_get_address(global.__DyCore_Video_FrameBuffer), buffSize, deltaTime);
    }
    else {
        updated = DyCore_video_get_frame(buffer_get_address(global.__DyCore_Video_FrameBuffer), buffSize);
    }

    if(updated || !surface_exists(global.__DyCore_Video_FrameSurface)) {
        buffer_set_used_size(global.__DyCore_Video_FrameBuffer, buffSize);
        var vw = DyCore_video_get_width();
        var vh = DyCore_video_get_height();
        if(!surface_exists(global.__DyCore_Video_FrameSurface) || vw != surface_get_width(global.__DyCore_Video_FrameSurface) || vh != surface_get_height(global.__DyCore_Video_FrameSurface)) {
            if(surface_exists(global.__DyCore_Video_FrameSurface))
                surface_free(global.__DyCore_Video_FrameSurface);
            global.__DyCore_Video_FrameSurface = surface_create(vw, vh);
        }

        buffer_set_surface(global.__DyCore_Video_FrameBuffer, global.__DyCore_Video_FrameSurface, 0);
        if(updated) global.__DyCore_Video_Preparing_Flag = false;
    }

    if(global.__DyCore_Video_Preparing_Flag)
        return -1;

    return global.__DyCore_Video_FrameSurface;
}

function dyc_video_draw(x, y, alp) {
    if(objMain.nowTime < 0) {
        global.__DyCore_Video_Preparing_Flag = true;
        draw_sprite_ext(sprBlack, 0, 0, 0, BASE_RES_W / 32, BASE_RES_H / 32, 0, c_black, alp);
        if(dyc_video_is_playing())
            dyc_video_pause();
        return;
    }

    if(!dyc_video_is_playing() && objMain.nowPlaying && editor_get_editmode() == 5 && 
        objMain.nowTime < dyc_video_get_duration() * 1000 - 200) {
        show_debug_message("-- Resuming video playback.");
        show_debug_message("-- nowTime: " + string(objMain.nowTime) + " ms, video duration: " + string(dyc_video_get_duration() * 1000) + " ms.");
        dyc_video_play();
        dyc_video_seek_to(objMain.nowTime / 1000);
    }

    var _surf = dyc_video_get_frame();

    if(_surf < 0) {
        draw_sprite_ext(sprBlack, 0, 0, 0, BASE_RES_W / 32, BASE_RES_H / 32, 0, c_black, alp);
        return;
    }
    var _sw = surface_get_width(_surf), _sh = surface_get_height(_surf);
    var _nw = BASE_RES_W, _nh = BASE_RES_H;
    var _scl = max(_nw / _sw, _nh / _sh); // Centre & keep ratios
    var _sx = (_nw - _sw * _scl) / 2, _sy = (_nh - _sh * _scl) / 2;

    shader_set(shd_video);
    draw_surface_ext(_surf, _sx, _sy, _scl, _scl, 0, c_white, alp);
    shader_reset();
}

function dyc_video_free() {
    var _savedSurface = global.__DyCore_Video_FrameSurface;
    var _savedBuffer = global.__DyCore_Video_FrameBuffer;
    DyCore_video_close();
    if(_savedSurface > 0)
        surface_free(_savedSurface);
    if(_savedBuffer > 0)
        buffer_delete(_savedBuffer);
    global.__DyCore_Video_FrameSurface = -999;
    global.__DyCore_Video_FrameBuffer = -1;
}

function dyc_video_seek_to(time) {
    if(editor_get_editmode() != 5)
        return;
    DyCore_video_seek(time);
}

// If the video is loaded
function dyc_video_is_loaded() {
    return DyCore_video_is_loaded();
}

function dyc_video_is_playing() {
    return DyCore_video_is_playing();
}

function dyc_video_pause() {
    DyCore_video_pause();
}

function dyc_video_play() {
    DyCore_video_play();
}

function dyc_video_set_speed(speed) {
    DyCore_video_set_speed(speed);
}

/// @description Set sync mode for video decoding.
/// @param {boolean} mode - True to enable sync mode, false to disable.
function dyc_video_set_sync_mode(mode) {
    DyCore_video_set_sync_mode(mode ? 1 : 0);
}

function dyc_video_get_sync_mode() {
    return DyCore_video_get_sync_mode() != 0;
}

function dyc_video_get_duration() {
    return DyCore_video_get_duration();
}

/// @description Get the current position of the video in seconds.
function dyc_video_get_position() {
    return DyCore_video_get_position() / 1000000;
}