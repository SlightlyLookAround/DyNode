var _editMode = editor_get_editmode();

// Difficulty-diff preview ghost: fixed faded alpha, no interaction, no pool sync.
if(variable_instance_exists(id, "isDiffPreview") && isDiffPreview) {
    image_alpha = 0.3;
    lastAlpha = 0.3;
    animTargetA = 0.3;
    animTargetLstA = 0.3;
    drawVisible = true;
    _prop_init(true);
    if(noteType == 2)
        pHeight = max(originalHeight,
            objMain.playbackSpeed * max(lastTime, 0) + dFromBottom + uFromTop);
    exit;
}

// If notes are being dragged, prevent pulling properties
if(_editMode < 5) {
    if(can_pull())
        pull_prop();
}

_prop_init();

if(stateType == NOTE_STATES.OUT && image_alpha<EPS) {
	drawVisible = false;
}
else
    drawVisible = true;

if(_editMode < 5) {
    selectInbound = editor_select_is_area() && editor_select_inbound(x, y, side, noteType, side);
    selectTolerance = selectInbound || stateType == NOTE_STATES.SELECTED;
}
else {
    selectInbound = false;
    selectTolerance = false;
}

if(_editMode == 5) {
    set_state(NOTE_STATES.OUT);
}

state();

selectUnlock = false;

if(drawVisible || nodeAlpha>EPS || infoAlpha>EPS || image_alpha>EPS) {
    if(_editMode < 5) {
        var _factor = 1;
        if(objMain.fadeOtherNotes && !editor_editside_allowed(side))
            _factor = 0.5;
        if(stateType == NOTE_STATES.ATTACH || stateType == NOTE_STATES.SELECTED) {
            if(image_alpha > 0.99)
                image_alpha = animTargetA;      // Prevent weird fade in when attaching notes are reset.
            if(infoAlpha < 0.01)
                infoAlpha = animTargetInfoA;
            if(nodeAlpha < 0.01)
                nodeAlpha = animTargetNodeA;
        }
        image_alpha = lerp_a(image_alpha, animTargetA * _factor,
            animSpeed * (objMain.nowPlaying ? objMain.musicSpeed * animPlaySpeedMul : 1));

        lastAlpha = lerp_a(lastAlpha, animTargetLstA * _factor,
            animSpeed * (objMain.nowPlaying ? objMain.musicSpeed * animPlaySpeedMul : 1));
        
        if(keycheck(ord("A")) || keycheck(ord("D")) || 
            objMain.topBarMousePressed || (side == 0 && objMain.nowPlaying)) {
            image_alpha = animTargetA * _factor;
            lastAlpha = animTargetLstA * _factor;
        }
        
        nodeAlpha = lerp_a(nodeAlpha, animTargetNodeA, animSpeed);
        infoAlpha = lerp_a(infoAlpha, animTargetInfoA, animSpeed);
        nodeBorderAlpha = lerp_a(nodeBorderAlpha, animTargetNodeBorderA, animSpeed);
    }
    else {
        image_alpha = animTargetA;
        lastAlpha = animTargetLstA;
        nodeAlpha = animTargetNodeA;
        infoAlpha = animTargetInfoA;
        nodeBorderAlpha = animTargetNodeBorderA;
    }
}

// If no longer visible then deactivate self
if(_editMode == 5 || (!drawVisible && nodeAlpha < EPS && infoAlpha < EPS && !note_is_activated(finst))) {
	note_deactivate_instance(id);
	return;
}

// Update Highlight Line's Position
if(_editMode < 5 && objEditor.editorHighlightLine && note_is_activated(id)) {
	if(stateType == NOTE_STATES.SELECTED && isDragging || stateType == NOTE_STATES.ATTACH_SUB || stateType == NOTE_STATES.DROP_SUB
		|| ((stateType == NOTE_STATES.ATTACH || stateType == NOTE_STATES.DROP) && id == editor_get_note_attaching_center())) {
		objEditor.editorHighlightTime = time;
        objEditor.editorHighlightPosition = position;
        objEditor.editorHighlightSide = side;
        objEditor.editorHighlightWidth = width;
        if(stateType == NOTE_STATES.ATTACH_SUB || stateType == NOTE_STATES.DROP_SUB) {
			objEditor.editorHighlightTime = time + lastTime;
        }
	}
}

// Add selection blend

if(stateType == NOTE_STATES.SELECTED)
    image_blend = selBlendColor;
else
    image_blend = c_white;